# Higher order functions

## Fold

Folds are used to process data structures to a single return value.  Some
languages call it *reduce*.

## Imperative example

Let's start with an example in python where fold could be used.
``` python
evens_as_string = ""
for num in range(6):
  if num%2 == 0:
    evens_as_string = evens_as_string + str(num)

print(evens_as_string)
```
This small snippet converts even numbers to string and concatenates them.  It
does two things:

 * iterates over a data structure
 * accumulates a value as it iterates through the structure

## Foldr

Let's have a look at the fold right function.

``` haskell
foldr :: Foldable t => (a -> b -> b) -> b -> t a -> b
```

The foldable typeclass describes types that can be folded.  List is an instance
of the foldable typeclass.  Replacing *t a* with *[a]* might help in reading the
type signature.

Let's go through the parameters one-by-one:

  * *(a -> b -> b)* : This is a function that accepts a value from the foldable
    and a value that has the same type as the result of the fold.  It combines
    one element of the data structure with the values processed so far.
  * *b* : This is the type that is the result of the fold.  This value is used
    by the combinator function the first time it is called.
  * *t a* : This is the data structure that we are folding.

Let's see the python code translated to Haskell using foldr.

``` haskell
f :: (Show a, Integral a) => a -> String -> String
f n acc
  | even n = (show n) ++ acc
  | otherwise = acc

foldr f "" [1..6]
```

The combinator function converts the content of the foldable to string and
concatenates it to the already combined value.  The second parameter of foldr
(the empty String) is combined with the last item of the foldable.

So foldr does something like this:

``` haskell
--foldr
(show 2 ++ (show 4 ++ (show 6 ++ "!")))
```

## Exercise
  * Create a list with type *[Knight]* and using foldr implement the following
    function.

``` haskell
concatColours :: Foldable t => t Knight -> String
```

## Foldl

Let's compare the type of left fold with the type of right fold.

``` haskell
foldr :: Foldable t => (a -> b -> b) -> b -> t a -> b
foldl :: Foldable t => (b -> a -> b) -> b -> t a -> b
```

The only visible difference is in the type of the combinator function.  The
function's first parameter is the already combined value and the second
parameter is the next value from the foldable.  Now let's see how it works.

Try out the following in ghci.
``` haskell
f :: (Show a, Integral a) => a -> String -> String
f n acc
  | even n = (show n) ++ acc
  | otherwise = acc

g :: (Show a, Integral a) => String -> a -> String
g acc n
  | even n = acc ++ (show n)
  | otherwise = acc

foldr f "" [1..6]
foldl g "" [1..6]
```

*g* is the same function as *f* with swapped parameters to conform to *foldl*
and accumulator is on the left side of concatenation operator.  To emphasize the
difference between the two folds, let's run the following.

``` haskell
foldr f "!" [1..6]
foldl g "!" [1..6]
```

So foldl would do something like this:

``` haskell
-- foldl
((("!" ++ show 2) ++ show 4) ++ show 6)

--foldr
(show 2 ++ (show 4 ++ (show 6 ++ "!")))
```

## Exercise
 * Write the *mysum :: (Num a, Foldable t) => t a -> a* function.

## Which fold should I use?

It is a hard question, and I can not give a complete answer to it because we
have not talked about lazy evaluation, which is a key factor.  For now follow
these guidelines:

 * If the foldable is an infinite structure you should use *foldr*.  We have not
   seen any infinite data structure so far.  You can start playing in ghci with
   *repeat, take, cycle* to have a basic understanding.
 * If the foldable is finite and it needs to be evaluated always completely,
   import *Data.List* and use *foldl'* which is identical to foldl.

