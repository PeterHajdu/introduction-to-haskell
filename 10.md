# Polymorphism

So far when we defined functions, we defined specific types for each parameter
and return value.

``` haskell
perimeter :: Shape -> Double
```

The perimeter function can be called with an argument of type *Shape* and
returns a value of type *Double*.  With polymorphism we can write functions and
data structures that can be used with many types instead of defining one
specific type.

## Parametric polymorphism

Let's write a function that always returns its only argument.

``` haskell
identityInt :: Int -> Int
identityInt x = x
```

Unfortunately this function can be used only with an *Int* value.  If I need the
same functionality for Strings I would have to implement the same function with
the same body.

``` haskell
identityString :: String -> String
identityString x = x
```

Clearly this is not a viable option.

Haskell allows us to define a function not only with specific types but *type
variable*s.  *Type variable*s are created in a function or data type declaration
with words starting with lower case letters.  These variables can be substituted
by the compiler with any other type.

``` haskell
identity :: a -> a
identity x = x
```

Instead of using a specific type like *Int* in the declaration of identity, I
used the type variable *a*.  *a* can be substituted with any other type.  The
only restriction this type declaration tells us is that the function has to
return a value of the same type as the parameter.

There is one interesting thing that might not be obvious at first sight.  There
is only one possible implementation of a function with this type.

``` haskell
fun :: a -> a
```

Since we do not know anything about the type *a*, we can not call any functions
on it.  Since we do not know how to construct a value of type *a* the only thing
we can return of type *a* is the only value of *a* that we know of, and that is
the parameter of *fun*.

### Polymorphic algebraic data types

Algebraic data types can have type parameters as well.  Let's have a look at the
Container type.

``` haskell
data Container a = Item a
```

Notice that the type constructor part, left of the *=* sign contains a type
parameter.  *Container* is not a complete type but only a constructor without
defining the *"a"* type parameter.  *Container Int* is a complete type.

Values of *Container a* hold a value of an arbitrary type.  Values are
constructed with the *Item* data constructor.  The only parameter of *Item* has
to be of the same type that we used as the type parameter of *Container*.

## Exercise
 * Have a look at the type of different *Container* values in ghci.

``` haskell
> :t Item "alma"
> :t Item True
```

### More type variables

``` haskell
someOtherFunction :: a -> b -> a
```

*someOtherFunction* has two parameters.  The second parameter's type does not
need to be the same as the first parameter's type, although it can be.  The type
of the return value has to be the same as the first parameter's type.

## Exercise
 * Implement the following function: *fun1 :: a -> b -> a*
 * Implement the following function: *fun2 :: a -> b -> b*
 * Give the two possible implementations of function: *fun3 :: a -> a -> a*
