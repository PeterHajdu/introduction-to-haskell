# Revisiting Either

This time we will rewrite the example from the previous lecture using Either.
We start with the types again.

``` haskell
(>>=) :: Maybe a -> (a -> Maybe b) -> Maybe b
(>>)  :: Maybe a -> Maybe b -> Maybe b
return :: a -> Maybe a
```

It is however not straight forward to translate the type to *Either*.  *Either*
differs from *IO* and *Maybe* in the number of type parameters.  While the
latter two has only one type parameter *Either* needs two parameters.

``` haskell
data Maybe a = Nothing | Just a
data Either a b = Left a | Right b
```

We need to transform *Either a b* to expect only one type parameter instead of
two.  If we bind one type parameter on Either you will get a type constructor
that expects only one type parameter.

``` haskell
data ErrorType = ...
type ErrorOr a = Either ErrorType a

(>>=) :: ErrorOr a -> (a -> ErrorOr b) -> ErrorOr b
(>>)  :: ErrorOr a -> ErrorOr b -> ErrorOr b
return :: a -> ErrorOr a
```

Let's rewrite the example from the previous lecture using *Either*.

``` haskell
import Text.Read

data AgeError = InvalidFormat
              | NotEnoughData
              | InvalidMeanAge
              deriving Show

safeMean :: [Int] -> Either AgeError Int
safeMean [] = Left NotEnoughData
safeMean lst = Right ((sum lst) `div` (length lst))

data Age = Age Int deriving (Eq, Show)

toAge :: Int -> Either AgeError Age
toAge age
  | age < 0 = Left InvalidMeanAge
  | age > 400 = Left InvalidMeanAge
  | otherwise = Right (Age age)

readIntList :: String -> Either AgeError [Int]
readIntList str = case readMaybe str of
                    Nothing -> Left InvalidFormat
                    Just lst -> Right lst

validateMeanAge :: String -> Either AgeError Age
validateMeanAge str = (readIntList str) >>= safeMean >>= toAge
```

Try out *validateMeanAge*.

``` haskell
validateMeanAge "Nobody expects the Spanish Inquisition!"
validateMeanAge "[]"
validateMeanAge "[-10, -20]"
validateMeanAge "[10, 20]"
```

## Exercise
  * Since all of the necessary operators are implemented for Either, you can
    use *do* notation.  Rewrite *validateMeanAge* using *do* notation.
  * Rewrite the case expression in *readIntList* to use the
    *maybe :: b -> (a -> b) -> Maybe a -> b* function.
  * Have a look at the *either :: (a -> c) -> (b -> c) -> Either a b -> c*
    function.  What does it do?
 
